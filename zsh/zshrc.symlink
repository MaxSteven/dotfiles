# shortcuts
export DOTFILES=$HOME/.dotfiles
export ZSH=$DOTFILES/zsh

# adding path directory for custom scripts
export PATH=$DOTFILES/bin:$PATH

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# - the code folder that we can `c [tab]` to
if [[ -d ~/code ]]; then
    export CODE_DIR=~/code
fi

# Find zsh files
typeset -U config_files
# config_files=($ZSH/**/*.zsh) # all zsh files
config_files=($DOTFILES/*/*.zsh) # top-level zsh files

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

# add each topic folder to fpath so that they can add functions and completion scripts
for topic_folder ($DOTFILES/*) if [ -d $topic_folder ]; then  fpath=($topic_folder $fpath); fi;

unset config_files
